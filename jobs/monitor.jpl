#!/usr/bin/env groovy

/*
  Copyright (C) 2018-2022 Collabora Limited
  Author: Guillaume Tucker <guillaume.tucker@collabora.com>

  This module is free software; you can redistribute it and/or modify it under
  the terms of the GNU Lesser General Public License as published by the Free
  Software Foundation; either version 2.1 of the License, or (at your option)
  any later version.

  This library is distributed in the hope that it will be useful, but WITHOUT
  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
  FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
  details.

  You should have received a copy of the GNU Lesser General Public License
  along with this library; if not, write to the Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
*/

/* ----------------------------------------------------------------------------
 * Jenkins parameters

The following parameters need to be defined in the Jenkins pipeline job, with
typical default values in brackets:

CONFIG_LIST
  List of build configs to check instead of all the ones in build-configs.yaml
KCI_API_URL (https://api.kernelci.org)
  URL of the KernelCI backend API
KCI_API_TOKEN_ID
  Identifier of the KernelCI backend API token stored in Jenkins
KCI_STORAGE_CONFIG (storage.kernelci.org)
  Name of the KernelCI storage configuration
DOCKER_BASE (kernelci/)
  Dockerhub base address used for the build images
*/

@Library('kernelci') _
import org.kernelci.util.Job

def updateLastCommit(config, commit) {
    withCredentials([string(credentialsId: params.KCI_API_TOKEN_ID,
                            variable: 'SECRET')]) {
        sh(script: """\
kci_build \
update_last_commit \
--build-config=${config} \
--commit=${commit} \
--api=${params.KCI_API_URL} \
--db-token=${SECRET} \
""")
    }
}

def checkConfig(config) {
    def retry = 3
    def commit = null

    while (retry--) {
        try {
            commit = sh(
                script: """
kci_build \
check_new_commit \
--build-config=${config} \
--storage-config=${params.KCI_STORAGE_CONFIG} \
""", returnStdout: true).trim()
            retry = 0
        } catch (error) {

            if (retry) {
                print("Failed to check ${config}, retyring: ${error}")
                sleep(1)
            } else {
                print("ERROR: All attempts failed with ${config}")
            }
        }
    }

    if (!commit) {
        print("${config}: no new commit")
        return
    }

    print("${config}: triggering build with commit ${commit}")

    updateLastCommit(config, commit)

    def job = "kernel-build-trigger"
    def str_params = [
        'BUILD_CONFIG': config,
        'COMMIT_ID': commit,
    ]
    def job_params = []
    def j = new Job()
    j.addStrParams(job_params, str_params)

    build(job: job, parameters: job_params, propagate: false, wait: false)
}

node("docker && monitor") {
    def j = new Job()
    def docker_image = "${params.DOCKER_BASE}kernelci"

    print("""\
    Storage:   ${params.KCI_STORAGE_CONFIG}
    Container: ${docker_image}""")

    j.dockerPullWithRetry(docker_image).inside('--init') {
        stage("Monitor") {
            def config_list = null
            def config_jobs = [:]

            if (params.CONFIG_LIST != "") {
                config_list = params.CONFIG_LIST.tokenize(' ')
            } else {
                def raw_configs = sh(script: "kci_build list_configs",
                                     returnStdout: true).trim()
                config_list = raw_configs.tokenize('\n')
            }

            for (String config: config_list) {
                def config_name = config
                config_jobs[config_name] = {
                    checkConfig(config_name)
                }
            }

            parallel(config_jobs)
        }
    }
}
